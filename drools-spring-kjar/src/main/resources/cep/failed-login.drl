package cep;

import bsep.tim4.hospitalApp.model.Log;
import bsep.tim4.hospitalApp.model.LogLevel;
import bsep.tim4.hospitalApp.model.LogAlarm;
import bsep.tim4.hospitalApp.model.LogAlarmType;

import java.util.Date;

rule "More than 5 login attempts in 1 minute from one username"
    when
        $log1: Log(type == "LOGIN_ERROR", $source: source, processed == false)
        accumulate(
            $log2: Log(
                this != $log1,
                type == "LOGIN_ERROR",
                source == $source,
                processed == false
            ) over window:time( 1m ),
            $logs: collectList($log2)
        )
        eval($logs.size() >= 5)
    then
        insert(new LogAlarm(new Date(), $source, LogAlarmType.FAILED_LOGIN, "Many login attempts"));
        modify($log1){setProcessed(true);};
        for (Log log: $logs) {
            modify(log){setProcessed(true);};
        }
end



